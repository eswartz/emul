\   video.fi
\ 
\   (c) 2010-2013 Edward Swartz
\ 
\   All rights reserved. This program and the accompanying materials
\   are made available under the terms of the Eclipse Public License v1.0
\   which accompanies this distribution, and is available at
\   http://www.eclipse.org/legal/epl-v10.html
\ 

0      RamVar  v-data

v-data

|+    +field  v-screen       \ VDP addr of screen
|+    +field  v-screensz     \ VDP size of screen
|+    +field  v-patts        \ VDP addr of patterns
|+    +field  v-pattsz       \ VDP size of pattern table
|+    +field  v-colors       \ VDP addr of colors
|+    +field  v-colorsz      \ VDP size of color table
|+    +field  v-sprites      \ VDP addr of sprites
|+    +field  v-sprcol       \ VDP addr of sprite color table (0 if not sprite 2 mode)
|+    +field  v-sprpat       \ VDP addr of sprite patterns
|+    +field  v-sprmot       \ VDP addr of sprite motion
|+    +field  v-free         \ usable space (if no pages)
      +field  v-pagesize     \ size of a page
    
|+    +1field v-text         \ set if mode is text-based
|+    +1field v-mono         \ set if monochrome (colors are global)
    
|+    +field  v-width        \ units across
|+    +field  v-height       \ units down
    
      +field  v-font         \ GROM offs of addr of current font
    
      +1field v-fntwidth     \ font width in pixels
      +1field v-fntheight    \ font height in pixels

      +1field  v-intflags    \ video interrupt flags
      +1field  v-blink       \ if set, set blink flag for text2 chars 
    
      +field  v-allocinfo    \ list of memory allocations for page 0
|+    +field  v-page         \ current page offset
      
      +1field v-rwpage       \ current r/w page
      +1field v-vispage      \ current visible page
      +1field v-pob          \ page offset bank -- for >64K access
      +1field v-unused
    
  4 +Nfield v-initpages    \ bitmask of pages that have been initialized for this mode
    
    +field  'v-setupregs    ( -- )
    +field  'v-drawchar     ( ch x y -- )
    +field  'v-readchar     ( x y -- ch )
    +field  'v-blinkcursor   ( x y -- )
    +field  'v-setupmode    ( -- )
    +field  'v-updatecolors ( -- )
    +field  'v-setfont      ( addr -- )
    +field  'v-clear        ( w h x y -- )
    +field  'v-hchar        ( ch len x y -- )
    +field  'v-vchar        ( ch len x y -- )
	+field  'v-readpixel    ( x y -- )
	+field  'v-drawpixel    ( c x y -- )
	+field  'v-drawline     ( xt x0 y0 x1 y1 -- )
	
|+  +field  v-stride        \ row stride in bytes
    +field  msx-shift       \ shift for pixel >> byte
    +field  msx-mask        \ mask for pixel of byte
    +field  msx-colmask     \ mask for available colors

    +field  msx-rowsperbankmask  \ mask for detecting bank crossings  
    
    20 +Nfield v-regsave    \ saved registers

dup constant v-data0

dup v-data - negate  Constant v-data-size

ramptr ! 


Create video-modes
    ' text-mode ,
    ' gfx-mode ,
    ' bit-mode ,
    ' gfx3-mode ,
    ' gfx4-mode ,
    ' gfx5-mode ,
    ' gfx6-mode ,
    ' gfx7-mode ,
    ' text2-mode ,
    ' mono-mode ,
    ' multi-mode ,
    0 ,
    
1 <export
$000    constant    text
$001    constant    gfx
$002    constant    bitmap      $002    constant    gfx2
$003    constant    gfx3           
$004    constant    gfx4
$005    constant    gfx5
$006    constant    gfx6
$007    constant    gfx7
$008    constant    text2
$009    constant    mono
$00A    constant    multi
export>

$00B    constant    last-video-mode

\ -------------------------------------------------------------------------


\   Set the VDP RAM bank.
\
\   This directly sets VR14 without saving it off (unlike vwreg).
\   It does honor the page offset, though.
\
: vsetbank  ( vaddr -- addr-16k )
    dup 14 urshift 
    v-pob c@  +
    $8E00 OR  VDPWA !
    $3fff AND
;


:   >vr         ( val reg -- )
    dup 20 <  if  v-regsave + c!  else 2drop then    
;

:   v-regaddr ( reg -- )
    dup  
    vwaddr
    dup $9000 < if
        $7fff and w>l.h   >vr
    else
        drop
    then
;

1 <export

:   (vr!)       ( val reg -- )
    2dup >vr
    
    \ write VDP addr
    $8000  swap l.h>w   OR  vwaddr
;


:   (vr@) ( reg# -- val )
    v-regsave + c@
;

export>

:   write-vregs ( reglist -- )
    begin 
        dup @ dup
    while
        v-regaddr
        2+
    repeat   
    2drop
;


:   write-vmoderegs ( r0|r1 -- )
    $100 u/mod   $8000 OR  v-regaddr
    $8100 OR  v-regaddr
;

: vid-show ( f -- )
    $40 1 (vr@)  bitset
    $8100 or  v-regaddr
;

\ ---------------------

: std-set-screen-reg
    v-screen @  10 URSHIFT
    v-pob c@  4 LSHIFT OR  
    2  (vr!) 
;

: std-set-color-reg
    v-colors @  6 URSHIFT   w>l.h
    v-pob c@  OR
    $7 and  10 (vr!)
            3  (vr!)
;

: std-set-patt-reg
    v-patts @  11 URSHIFT   
    v-pob c@  3 LSHIFT OR
    4 (vr!)
;

: std-set-sprite-reg
    v-sprites @  7 URSHIFT   w>l.h
    
    v-pob c@  1 URSHIFT OR
    11 (vr!)
    
    5  (vr!)
;

: std-set-sprpat-reg
    v-sprpat @  11 URSHIFT   
    v-pob c@  3 LSHIFT OR
    6 (vr!)
;


\ ---------------------

: >v-fontaddr   ( ch -- )
    v-fntheight c@ u*  v-font @ +
;

\ Save space for content under the blinking cursor 
8   RamVar v-curs-under  

1   RamVar v-curs                         \ state of cursor (0=off, $80=on)
1   RamVar v-cursor-timer                 \ current iter

20 Constant v-cursor-blink              \ 30/60 sec
 
:   write-var-list   ( table -- )
    begin
        dup @ 
    while
        dup  d@  swap !
        4 +
    repeat
    drop
;

:   v-set-alloc-list ( table -- )
    dup write-var-list
    v-allocinfo !
;

|+ : (mode)
    cursor-off

    dup  0 last-video-mode within  0= err-out-of-range ?throw
    cells  video-modes +  @ 
    
    dup 0= err-undefined ?throw
    
    v-font @  v-fntwidth c@  v-fntheight c@
    v-data v-data-size 0 -1  (cfill)     \ v-data is end 
    v-fntheight c!  v-fntwidth c!  v-font !

    ( mode ) execute
    
    0.0 v-initpages D!
    
    0 vpage    
;

1   RamVar fg
1   RamVar bg

: color-byte
    fg c@ 4 lshift  bg c@ $f and  or
;
: color-word
    bg c@  $ff and  fg c@  l.h>w
;

: v-refresh-colors
    'v-updatecolors @  execute
;

1 <export
: fg!
    $ff and  fg c!
    v-refresh-colors
;

: bg!
    $ff and  bg c!
    v-refresh-colors
;

: fg@
    fg c@
;   

: bg@
    bg c@
;

export>

\   Reset video state
\
\   -- reset terminal bounds
\   -- clear memory
\   -- load font (if needed)

Create msx-enhregs
    $8828 ,     \ 64k video RAM, color 0 from palette
    $8E00 ,     \ set bank 0, page 0, etc
    $8980 ,     \ 212-line mode, color, etc
    $8C00 ,     \ turn off alt colors
    $8D00 ,     \ turn off blink/pageswap
    $8F00 ,     \ point to sr0
    0 ,

|+ : (vreset)
    \ the chip itself is always enhanced
    msx-enhregs write-vregs

    'v-setupmode @  execute

    term-reset
    
    true vid-show
;             

|+ : vreset
    (vreset)

    v-refresh-colors
    
    (setfont)

    \ v-width @ v-height @  0 0  'v-clear @  execute    

    term-reset
    
    cls
;

|+ : vstdpal
    $9000 v-regaddr
    32 0 do 
        msx-stdpalette i + c@  VDPCL c!
    loop
;


:   v-cursor-off  ( x y -- )
    v-curs c@ if
        'v-blinkcursor @ execute
    else
        2drop
    then
;

:   update-cursor
    1 v-cursor-timer c+!
    v-cursor-timer c@  v-cursor-blink >= if
        0 v-cursor-timer c!
        $xy
        'v-blinkcursor @ execute
    then
;


\ ---------------------

: (sprite-setup)
    v-sprites @  $80   $00  +vfill
    v-sprpat @   $800  $00  +vfill
    v-sprmot @   $80   $00  +vfill
;

: std-sprite-setup
    (sprite-setup)
    $D0  v-sprites @ +vc!
;

: v9938-sprite-setup
    (sprite-setup)
    $D8  v-sprites @ +vc!
    v-sprcol @  $200  $00  +vfill
;


include video_text.fi
include video_gfx.fi
include video_bit.fi
include video_msx.fi


\ ---------------------

1 <EXPORT

:   mode ( num -- )
    (mode) vreset vstdpal
;


:   (setfont)
    v-font @  'v-setfont @  execute
    term-refresh
;

:   v-font! ( groffs -- graddr )
    g@  v-font !
;

| : font! ( groffs w h )
    v-fntheight c!  v-fntwidth c!
    v-font!
    (setfont)
;

:   font8x8
    >grom_font8x8  8  8 font!
;
:   font6x8
    >grom_font8x8  6  8 font!
;
:   font5x6
    >grom_font5x6  5  6 font!
;

: <video    ( -- flags )
    cursor-off
    v-intflags c@
    [ v-int-nosprmot v-int-nocursor or literal ] v-intflags c! 
;

: video>  ( oldflags -- )
    v-intflags c!
;

: vblink!  ( flag -- )
    0= invert  v-blink c!
;

( set the page where data is read/written:
    update v-pob and all the table addresses
    to match
)
:: vrwpage    ( pg -- )
    cursor-off
    
    \ write base values 
    v-allocinfo @  write-var-list

    \ reduce page to valid range    
    pg  
    $2.0000  v-pagesize @  ?dup if  UM/MOD  then  nip  
    UMOD
    dup  v-rwpage c!
    
    \ get page offset
    ( pg ) v-pagesize @ ?dup if  UM*  ( lo hi )  else  0 swap  then  
    
    \ bits A15-A14 are set via vsetbank, et al, into R14 automatically
    \ bit A16 is set in R14 via v-pob.  This is also applied when
    \ setting registers.
    $1 and  2 lshift  v-pob c!
    
    :> offs
    
    offs    v-page !
    offs    v-screen +!
    offs    v-patts +!
    offs    v-colors +!
    offs    v-sprites +!
    offs    v-sprmot +!
    offs    v-sprpat +!
    offs    v-sprcol +!
    
    \ init page if not yet set up
    1.  v-rwpage c@  s>d  dlshift  :> pmaskH :> pmaskL
    pmaskL pmaskH  v-initpages d@  DAND  D0=  if
        (vreset)
        pmaskL  v-initpages  |!
        pmaskH  v-initpages 2+  |!
    then
;

( set the visible page:
    for each table address for the given page,
    update video regs to match. 
)
:: vvispage  ( pg -- )
    \ save current values...
    v-screen @  v-patts @  v-colors @  
    v-sprites @  v-sprmot @  v-sprpat @  v-sprcol @
    v-pob c@  v-rwpage c@  
    
    pg  vpage 
        
    \ restore original values...
    v-rwpage c!  v-pob c!
    v-sprcol !  v-sprpat !  v-sprmot !  v-sprites !
    v-colors !  v-patts !  v-screen !
;

( set the visible and read/write page )
: vpage ( pg -- )
    vrwpage
    
    v-rwpage c@  v-vispage c!
    
    'v-setupregs @  execute
;

:   pixel!	( c x y -- )
    'v-drawpixel @  execute
;

:   pixel@	( x y -- c )
    'v-readpixel @  execute
;

:   (line) ( x0 y0 x1 y1 xt -- )
    'v-drawline @  execute
;

:	op-draw ( x y -- )
	fg@ rot rot pixel!
;

:	line ( x0 y0 x1 y1 xt -- )
	['] op-draw (line)
;

EXPORT>

::  slow-drawline   ( x0 y0 x1 y1 xt -- )
	x1 x0 - abs               :> dx
	1  x0 x1 > if negate then :> sx
	y1 y0 - abs negate        :> dy
	1  y0 y1 > if negate then :> sy
	dx dy +                   :> error
	0                         :> e2
	begin
		x0 y0  xt execute
		x0 x1 =  y0 y1 =  and  if exit then
		error 2*  to e2 
		e2 dy >= if
			x0 x1 =  if exit then
			error dy +  to error
			x0 sx +     to x0 
		then 	
		e2 dx <= if 
			y0 y1 =  if exit then 
			error dx +  to error 
			y0 sy +     to y0 
		then 	
	again
;

:   video-init
    \ reset latches
    VDPRD c@ drop
    0 0 gc!

    0 v-pob c!
    0 v-intflags c!
        
    text mode

    $1 fg!
    $A7 bg!     \ works for gfx7 mode
    
    font8x8    
    
;


